construct {
    ?p a tmp:Person ;
        rdfs:label ?name ;
        tmp:ptitle ?pTitle ;
        tmp:pic ?picture ;
        tmp:description ?desc ;
        tmp:email ?email ;
        tmp:phone ?phone ;
        tmp:position ?position .
    ?position rdfs:label ?title ;
	tmp:rank ?rank ;
        tmp:org ?org.
    ?org rdfs:label ?orgName .
    ?org tmp:parent ?parentOrg .
    ?parentOrg rdfs:label ?parentOrgName .
}
where {
    {
        ?p a foaf:Person ;
            rdfs:label ?name ;
            fhd:sortLetter ?startswith ;
            vivo:relatedBy ?position .
        OPTIONAL {?p fhd:image ?picture }
        OPTIONAL {?p fhd:briefDescription ?desc }
        ?position a vivo:Position ;
                rdfs:label ?title ;
            vivo:rank ?rank ;
                vivo:relates ?org .
        ?org a foaf:Organization ;
             rdfs:label ?orgName .
        OPTIONAL {
            ?parentOrg obo:BFO_0000051 ?org ;
                       rdfs:label ?parentOrgName .
        }
        FILTER NOT EXISTS { ?position a fhd:FormerPosition }
    }
    UNION {
        ?p a foaf:Person ;
            fhd:sortLetter ?startswith .
        ?p obo:ARG_2000028 ?vci .
        ?vci vcard:hasEmail ?ec .
        ?ec vcard:email ?email .
    }
    UNION {
        ?p a foaf:Person ;
            fhd:sortLetter ?startswith .
        ?p obo:ARG_2000028 ?vci .
        ?vci vcard:hasTelephone ?pc .
        ?pc vcard:telephone ?phone .
    }
    UNION {
        ?p a foaf:Person ;
            fhd:sortLetter ?startswith .
        ?p obo:ARG_2000028 ?vci .
        ?vci vcard:hasTitle ?ti .
        ?ti vcard:title ?pTitle .
    }

}
